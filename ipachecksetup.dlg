*! Version 1.0.0 Ishmail Azindoo Baako and Mehrab Ali 19Sep2020
* Originally created by Ishmail Azindoo Baako (IPA) 24feb2018

VERSION 12.0

POSITION . . 600 400

DIALOG main, label("ipachecksetup - Prefill IPA Check inputs") tabtitle("Main")
BEGIN
  TEXT     tx_xls   10   10	  120	 ., label(Select XLS Form*)
  FILE 	   fl_xls 	10	 +20	580	 ., label(Browse) error(select xls form)  dialogtitle(Select XLS Form)
  TEXT     tx_tpl   10   +30  400  ., label(Select HFC inputs template)
  FILE     fl_tpl   10   +20  580  ., label(Browse)  dialogtitle(Select IPA HFC template)
  TEXT	   tx_out		10	 +30  400	 ., label(Save output as*)
  FILE 	   fl_out		10	 +20  580  ., label(Save As) error(specify output file) save
  CHECKBOX cb_rep   20   +40  .    ., label(replace) option(replace) 
  TEXT     tx_pre   250   @  .  ., label(Prefix*) 
  EDIT     st_prefix 300   @  210    ., label(Prefix) 
  FRAME    box_fmt  10   +40  230 170, error(select eatiher wide or long)
    TEXT     tx_fmt   20   +10  210    ., label(Select whether data is in long or wide:)
    RADIO    rd_fmt_l 30   +30  100    ., first label("Long") option(long)
    RADIO    rd_fmt_w 30   +20  @  ., last label("Wide") option(wide)
    TEXTBOX     tx_label   20   +30  210    40, label(Specify label language: (keep blank if language not specified in XLSForm))
    EDIT     st_label 20   +50  210    ., label(Specify label language) 
   
  GROUPBOX    box_ins  250   203  330 170, label(Instructions) 
    TEXT     tx_insone 270   +20  300    ., label(1. Fill in the main tab) 
    TEXT     tx_instwo 270   +20  300    ., label(2. Fill in the advanced tab for other options)
    TEXTBOX  tx_insgen 270   +40  300    50, label(Read further about HFC excel input file at IPA github page: https://github.com/PovertyAction/high-frequency-checks)
END

DIALOG advanced, label("test") tabtitle("Advanced")
BEGIN
  GROUPBOX    gbox_ad  10   10  200 110, label("Specify missing values")
    SPINNER   dk   120   +30   60    ., label(Don't know:) tooltip("Default is -999") default(-999) min(-9999999)
    TEXT   tx_dk   20   @   100    ., label(Don't know:) 
    SPINNER   osp   120   +20   60    ., label(Others specify:) tooltip("Default is -666") default(-666) min(-9999999)
    TEXT   tx_osp   20   @   100    ., label(Others specify:) 
    SPINNER   rf   120   +20   60    ., label(Refusal:) tooltip("Default is -888") default(-888) min(-9999999)
    TEXT   tx_rf     20   @   100    ., label(Refusal:) 
END

OK ok1,      label("OK")
CANCEL can1, label("Cancel")
HELP hlp1,   view("help ipachecksetup")
SUBMIT sub1, label("Submit")
RESET res1
COPY copy1

PROGRAM command
BEGIN
    * Program start
    put "ipachecksetup using "
   
    require main.fl_xls 
    put `"""' main.fl_xls `"""' 
	 
    * Outfile
    put ", outfile("
    require  main.fl_out
    put `"""' main.fl_out `"""' 
    
    * Prefix
    put ") prefix("
    require main.st_prefix
    put main.st_prefix
    put ") " 

    * Template
    if (main.fl_tpl) {
      put " template("
      put `"""'  main.fl_tpl `"""' ")" 
    }


    * Label
    if (main.st_label) {
      put " label("
      put `"""' main.st_label `"""' ")"
    }

        option main.cb_rep
        option main.rd_fmt_w
        option main.rd_fmt_l
END
