*! Version 1.0.0 Ishmail Azindoo Baako and Mehrab Ali 19Sep2020
* Originally created by Ishmail Azindoo Baako (IPA) 24feb2018

VERSION 12.0

POSITION . . 600 510

DIALOG main, label("ipachecksetup - Prefill IPA Check inputs") tabtitle("Main")
BEGIN
  TEXT     tx_xls   10   10	  120	 ., label(Select XLS Form*)
  FILE 	   fl_xls 	10	 +20	580	 ., label(Browse) error(select xls form)  dialogtitle(Select XLS Form) 
  TEXT     tx_tpl   10   +30  400  ., label(Select HFC inputs template)
  FILE     fl_tpl   10   +20  580  ., label(Browse)  dialogtitle(Select IPA HFC template)
  TEXT	   tx_out		10	 +30  400	 ., label(Save output as*)
  FILE 	   fl_out		10	 +20  580  ., label(Save As) error(specify output file) save
  CHECKBOX cb_rep   20   +40  .    ., label(replace) option(replace) 
  TEXT     tx_pre   250   @  .  ., label(Prefix*) 
  EDIT     st_prefix 300   @  210    ., label(Prefix) 
  FRAME    box_fmt  10   +80  230 170, error(select eatiher wide or long)
    TEXT     tx_fmt   20   +10  210    ., label(Select whether data is in long or wide:)
    RADIO    rd_fmt_l 30   +30  100    ., first label("Long") option(long)
    RADIO    rd_fmt_w 30   +20  @  ., last label("Wide") option(wide)
    TEXTBOX     tx_label   20   +30  210    40, label(Specify label language: (keep blank if language not specified in XLSForm))
    EDIT     st_label 20   +50  210    ., label(Specify label language) 
   
  GROUPBOX    box_ins  250   230  330 210, label(Instructions) 
    TEXTBOX     tx_insone 270   +20  300    ., label(1. Fill in the main tab. HFC inputs template is optional.) 
    TEXTBOX     tx_instwo 270   +20  300    ., label(2. Fill in the advanced tab for other options. All the options are optional.)
    TEXTBOX  tx_insgen 270   +50  300    50, label(Read further about HFC excel input file at IPA github page: https://github.com/PovertyAction/high-frequency-checks)
    TEXTBOX  tx_inspr  270   +60  300    50, label(Read detailed instructions for this program at IPA github page: https://github.com/PovertyAction/ipachecksetup)
END

DIALOG advanced, label("test") tabtitle("Advanced")
BEGIN
  GROUPBOX    gbox_path  10   10  580 110, label("Files and folders")
    TEXT   tx_su   20   +20   200    ., label(Select survey dataset) 
    FILE   fl_su   180   @   400  ., label(Browse)  dialogtitle(Select survey dataset (dta file))
    TEXT   tx_med   20   +30   200    ., label(Select media directory) 
    FILE   fol_med   180   @   400  ., label(Browse)  dialogtitle(Select media directory) directory
    TEXT   tx_bc   20   +30   200    ., label(Select backcheck XLSForm) 
    FILE   fl_bc   180   @   400  ., label(Browse)  dialogtitle(Select backcheck XLSForm)

  GROUPBOX    gbox_mi  10   +30  200 110, label("Specify missing values")
    SPINNER   dk   120   +30   60    ., label(Don't know:) tooltip("Default is -999") default(-999) min(-9999999)
    TEXT   tx_dk   20   @   100    ., label(Don't know:) 
    SPINNER   osp   120   +20   60    ., label(Others specify:) tooltip("Default is -666") default(-666) min(-9999999)
    TEXT   tx_osp   20   @   100    ., label(Others specify:) 
    SPINNER   rf   120   +20   60    ., label(Refusal:) tooltip("Default is -888") default(-888) min(-9999999)
    TEXT   tx_rf     20   @   100    ., label(Refusal:) 

  GROUPBOX    gbox_id  220   -70  370 110, label("ID variables")
    TEXT      tx_sid   230   +20  90    40, label(Survey ID(s))
    EDIT      st_sid   310   @  80    ., label(Survey ID) 
    TEXT      tx_eid   230   +30  90    40, label(Enumerator ID)
    EDIT      st_eid   310   @  80    ., label(Enumerator ID) 
    TEXT      tx_tid   230   +30  90    40, label(Team ID)
    EDIT      st_tid   310   @  80    ., label(Team ID)
    /* backcheck ids */
    TEXT      tx_beid   +90   -60  90    40, label(Back Checker ID)
    EDIT      st_beid   +100   @  80    ., label(Back Checker ID) 
    TEXT      tx_btid   -100   +30  90    40, label(BC Team ID)
    EDIT      st_btid   +100   @  80    ., label(BC Team ID) 

  GROUPBOX    gbox_con  10   +60  200   110, label("Constraints and outliers")
    TEXT      tx_mul    20   +20  70    ., label(Multiplier) 
    EDIT      mul       80    @   50    ., label(Multiplier) tooltip("Default is 3") default(3)  numonly
    CHECKBOX  cb_sd    140    @   .     ., label("    SD") default(1)
    TEXT      tx_min    20   +30  70    ., label(Soft min) 
    EDIT      min       80    @   50    ., label(Soft min) tooltip("Default is 10") default(10)  numonly
    TEXT      tx_min_p  140   @   10     ., label("%")
    TEXT      tx_max    20   +30  70    ., label(Soft max) 
    EDIT      max       80    @   50    ., label(Soft max) tooltip("Default is 10") default(10)  numonly
    TEXT      tx_max_p  140   @   10     ., label("%")

  TEXT    tx_date    10   +35  210    ., label(Survey Start date (MM/DD/YYYY))
  EDIT    ed_date    10   +20  170    ., label(Start date)

  GROUPBOX    gbox_cons  220   230  170   170, label("Consent variables and values")
    TEXT      tx_con    230   +20  50    ., label(Variable)
    TEXT      tx_con_v  300    @   40    ., label(Value) 
    EDIT      con_i       230   +20  70    ., label(con_1)
    EDIT      con_v_i     300    @   50    ., label(consent value 1) 
    EDIT      con_ii       230   +20  70    ., label(con_2)
    EDIT      con_v_ii     300    @   50    ., label(consent value 2) 
    EDIT      con_iii       230   +20  70    ., label(con_3)
    EDIT      con_v_iii     300    @   50    ., label(consent value 3) 
    EDIT      con_iv       230   +20  70    ., label(con_4)
    EDIT      con_v_iv     300    @   50    ., label(consent value 4) 
    EDIT      con_v       230   +20  70    ., label(con_5)
    EDIT      con_v_v     300    @   50    ., label(consent value 5) 
    EDIT      con_vi       230   +20  70    ., label(con_6)
    EDIT      con_v_vi     300    @   50    ., label(consent value 6) 

  GROUPBOX    gbox_comp  400   230  190   170, label("Complete variables and values")
    TEXT      tx_com    420   +20  50    ., label(Variable)
    TEXT      tx_com_v  480    @   40    ., label(Value) 
    EDIT      com_i       420   +20  70    ., label(com_1)
    EDIT      com_v_i     491    @   50    ., label(complete value 1) 
    EDIT      com_ii       420   +20  70    ., label(com_2)
    EDIT      com_v_ii     491    @   50    ., label(complete value 2) 
    EDIT      com_iii       420   +20  70    ., label(com_3)
    EDIT      com_v_iii     491    @   50    ., label(complete value 3) 
    EDIT      com_iv       420   +20  70    ., label(com_4)
    EDIT      com_v_iv     491    @   50    ., label(complete value 4) 
    EDIT      com_v       420   +20  70    ., label(com_5)
    EDIT      com_v_v     491    @   50    ., label(complete value 5) 
    EDIT      com_vi       420   +20  70    ., label(com_6)
    EDIT      com_v_vi     491    @   50    ., label(complete value 6) 

  TEXT      tx_rone    10   +50  180    ., label("Research oneway varlist")
  EDIT      rone       170   @   330    ., label("Research oneway varlist")

  TEXT      tx_rtwo    10   +30  180    ., label("Research twoway varlist") 
  EDIT      rtwo       170   @   330    ., label("Research twoway varlist") 

  TEXT      tx_rtwoby    10   +30  180    ., label("By variable") 
  EDIT      rtwoby       170   @   80    ., label("By variable")
END

OK ok1,      label("OK")
CANCEL can1, label("Cancel")
HELP hlp1,   view("help ipachecksetup")
SUBMIT sub1, label("Submit")
RESET res1
COPY copy1

PROGRAM command
BEGIN
    * Program start
    put "ipachecksetup using "
   
    require main.fl_xls main.fl_out main.st_prefix
    put `"""' main.fl_xls `"""' 
	 
    * Outfile
    put ", outfile("
    put `"""' main.fl_out `"""' 
    
    * Prefix
    put ") prefix("
    put main.st_prefix
    put ") " 

    * Template
    if (main.fl_tpl) {
      put " template("
      put `"""'  main.fl_tpl `"""' ")" 
    }


    * Label
    if (main.st_label) {
      put " label("
      put `"""' main.st_label `"""' ")"
    }

    * Survey
    if (advanced.fl_su) {
      put " survey("
      put `"""' advanced.fl_su `"""' ")"
    }

    * media
    if (advanced.fol_med) {
      put " media("
      put `"""' advanced.fol_med `"""' ")"
    }

    * osp
    if (advanced.tx_osp) {
      put " osp("
      put  advanced.tx_osp ")"
    }

    * Refusal
    if (advanced.tx_rf) {
      put " refusal("
      put advanced.tx_rf ")"
    }

    * Don't knkow
    if (advanced.tx_dk) {
      put " dontknow("
      put advanced.tx_dk ")"
    }

    * Consent 
    if advanced.con_i | advanced.con_ii | advanced.con_iii | advanced.con_iv | advanced.con_v | advanced.con_vi {
      put " consent("
      if (advanced.con_i) {
        require advanced.con_v_i
        put advanced.con_i " " advanced.con_v_i
      }

      if (advanced.con_ii) {
        require advanced.con_v_ii
        put ", " advanced.con_ii " " advanced.con_v_ii
      }

      if (advanced.con_iii) {
        require advanced.con_v_iii
        put ", " advanced.con_iii " " advanced.con_v_iii
      }

      if (advanced.con_iv) {
        require advanced.con_v_iv
        put ", " advanced.con_iv " " advanced.con_v_iv
      }

      if (advanced.con_v) {
        require advanced.con_v_v
        put ", " advanced.con_v " " advanced.con_v_v
      }

      if (advanced.con_vi) {
        require advanced.con_v_vi
        put ", " advanced.con_vi " " advanced.con_v_vi
      }

      put ")"  
    }

    * Incomplete 
    if advanced.com_i | advanced.com_ii | advanced.com_iii | advanced.com_iv | advanced.com_v | advanced.com_vi {
      put " incomplete("
      if (advanced.com_i) {
        require advanced.com_v_i
        put advanced.com_i " " advanced.com_v_i
      }

      if (advanced.com_ii) {
        require advanced.com_v_ii
        put ", " advanced.com_ii " " advanced.com_v_ii
      }

      if (advanced.com_iii) {
        require advanced.com_v_iii
        put ", " advanced.com_iii " " advanced.com_v_iii
      }

      if (advanced.com_iv) {
        require advanced.com_v_iv
        put ", " advanced.com_iv " " advanced.com_v_iv
      }

      if (advanced.com_v) {
        require advanced.com_v_v
        put ", " advanced.com_v " " advanced.com_v_v
      }

      if (advanced.com_vi) {
        require advanced.com_v_vi
        put ", " advanced.com_vi " " advanced.com_v_vi
      }

      put ")"  
    }
    
    * Survey ID
    if advanced.st_sid {
      put " id(" advanced.st_sid ")"
    }

    * Enumerator ID
    if advanced.st_eid {
      put " enumid(" advanced.st_eid ")"
    }

    * Team ID
    if advanced.st_tid {
      put " teamid(" advanced.st_tid ")"
    }
        option main.cb_rep
        option main.rd_fmt_w
        option main.rd_fmt_l

    * Survey Start date
    if advanced.ed_date {
      put " surveystart(" advanced.ed_date ")"
    }

    * Multiplier
    if advanced.mul {
      put " multiplier(" advanced.mul 

      if advanced.cb_sd {
        put ", sd"
      }
      put ")"
    }

    * Softmin and max
    put " softmin(" advanced.min ")"
    put " softmax(" advanced.max ")"

    * BCID
    if advanced.st_beid {
      put " backcheck(" advanced.st_beid ")"
    }

    * BC Team ID
    if advanced.st_btid {
      put " bcteamid(" advanced.st_btid ")"
    }

    * Backcheck
    if advanced.fl_bc {
      put `" backcheck(""' advanced.fl_bc `"")"'
    }

    * Reaserch oneway
    if advanced.rone {
      put " r1(" advanced.rone ")"
    }

    * Reaserch twoway
    if advanced.rtwo {
      require advanced.rtwoby 
      put " r2(" advanced.rtwo ", " advanced.rtwoby ")"
    }
END


